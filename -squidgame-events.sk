on join:
    checkGameSettings(player) = true
    yaml value "BungeeCord" from "config" is true #faire ce check dans checkgamesettings?
    joinSquidgame(player)

on quit:
    {squidgame::status} is "queue" or "game"
    metadata "status" of player is "game" or "queue"
    set quit message to ""
    leaveSquidgame(player)

on weather change:
    {squidgame::status} is not "game"
    clear weather

on hunger level change:
    metadata "status" of player is set
    cancel event

on break:
    metadata "status" of player is set
    cancel event

on place:
    metadata "status" of player is set
    cancel event

on damage:
    if metadata "status" of attacker is "queue" or "spectator":
        cancel event
    else if metadata "status" of victim is "queue" or "spectator":
        cancel event
        
on death of player:
    {squidgame::status} is "game"
    metadata "status" of victim is "game"
    set death message to ""
    if attacker is a player:
        set {_message} to yml value "Message_PlayerEliminatedByPlayer" from "messages"
        replace all "{VICTIM}" in {_message} with "%victim%"
        replace all "{ATTACKER}" in {_message} with "%attacker%"
    else:
        set {_message} to yml value "Message_PlayerEliminated" from "messages"
        replace all "{VICTIM}" in {_message} with "%victim%"
        
    send "%yml value "Message_Logo" from "messages"% %{_message}% &f(&c%size of {squidgame::players::*}%&f/&c%yml value "Maximum_Player" from "config"%&f)" to {squidgame::players::*}
    set metadata "status" of victim to "spectator"
    force the victim to respawn

on respawn:
    if {squidgame::status} is "game":
        set {_loc} to metadata "location" of player
        teleport player to location(x-coordinate of {_loc}, y-coordinate of {_loc}+10, z-coordinate of {_loc}, world("%world of player%"))
        killPlayer(player)
    else:
        teleport player to {squidgame::mainLobby}

#on TabComplete:
#    if event-string contains "/squidgame":
#        if player has permission "%yml value "Permission_Squidgame" from "config%":
#            add "join" to completions
#            add "quit" to completions
#            add "setdormlobby" to completions
#        else:
#            add "join" to completions
#            add "quit" to completions